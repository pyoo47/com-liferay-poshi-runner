buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.defaults", version: "latest.release"
	}

	repositories {
		mavenLocal()

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: "com.liferay.root.defaults.plugin"
apply plugin: "java"

configurations {
	poshiRunner
}

dependencies {
	poshiRunner group: "com.liferay", name: "com.liferay.poshi.runner", version: "1.0.220"
}

repositories {
	mavenLocal()

	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

task checkPoshiRunnerExtPropertyFileNames
task executePQLQuery (type: JavaExec)
task expandPoshiRunner (type: Copy)
task help
task runPoshi(type: Test)
task validatePoshi (type: JavaExec)
task writePoshiProperties (type: JavaExec)

checkPoshiRunnerExtPropertyFileNames {
	if (!project.hasProperty("poshiRunnerExtPropertyFileNames")) {
		println "No poshi runner ext property file names provided, defaulting to null"

		project.ext.poshiRunnerExtPropertyFileNames = null
	}
	else {
		println "Poshi Runner ext property file names: "
		println poshiRunnerExtPropertyFileNames
	}
}

executePQLQuery {
	args('executePQLQuery')
	classpath = configurations.poshiRunner
	main = "com.liferay.poshi.runner.PoshiRunnerCommandExecutor"
	systemProperties getPoshiRunnerExtProperties(poshiRunnerExtPropertyFileNames)
	workingDir = "."
}

expandPoshiRunner {
	dependsOn clean

	File poshiRunnerJar = findPoshiRunnerJar()

	if (poshiRunnerJar == null) {
		throw new Exception("The Poshi Runner jar cannot be found from dependencies")
	}

	from zipTree(poshiRunnerJar)
	into "$buildDir/poshi-runner"
}

help {
	println "Format to execute poshi tasks: "
	println "./gradlew <task> -D<property=value>"
	println ""
	println "Format to provide property files for poshi tasks: "
	println "./gradlew -PposhiRunnerExtPropertyFileNames=file1,file2 <task>"
	println ""
	println "Run './gradlew tasks' to see the list of available tasks"
	println ""
}

runPoshi {
	dependsOn expandPoshiRunner

	testLogging.showStandardStreams = true

	classpath = configurations.poshiRunner
	include 'com/liferay/poshi/runner/PoshiRunner.class'
	scanForTestClasses = false
	systemProperties getPoshiRunnerExtProperties(poshiRunnerExtPropertyFileNames)
	testClassesDir = new File("$buildDir/poshi-runner")
}

validatePoshi {
	main = "com.liferay.poshi.runner.PoshiRunnerCommandExecutor"
	workingDir = "."
	classpath = configurations.poshiRunner
	args('validatePoshi')
	systemProperties getPoshiRunnerExtProperties(poshiRunnerExtPropertyFileNames)
}

writePoshiProperties {
	main = "com.liferay.poshi.runner.PoshiRunnerCommandExecutor"
	workingDir = "."
	classpath = configurations.poshiRunner
	args('writePoshiProperties')
	systemProperties getPoshiRunnerExtProperties(poshiRunnerExtPropertyFileNames)
}

def findPoshiRunnerJar() {
	Iterator<File> iterator = configurations.poshiRunner.iterator();

	while (iterator.hasNext()) {
		File file = iterator.next()

		String fileName = file.getName()

		if (fileName.startsWith("com.liferay.poshi.runner-")) {
			return file
		}
	}

	return null
}

def getPoshiRunnerExtProperties(poshiRunnerExtPropertyFileNames) {
	Properties poshiRunnerExtProperties = new Properties()

	if (poshiRunnerExtPropertyFileNames != null) {
		for (String poshiRunnerExtPropertyFileName : poshiRunnerExtPropertyFileNames.split(",")) {
			try {
				InputStream inputStream = new FileInputStream(
					poshiRunnerExtPropertyFileName)

				poshiRunnerExtProperties.load(inputStream)
			}
			catch (Exception e) {
				e.printStackTrace()

				throw e
			}
		}
	}

	poshiRunnerExtProperties.putAll(System.getProperties())

	return poshiRunnerExtProperties
}